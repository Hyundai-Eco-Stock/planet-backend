plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
}

group = 'org.jeayoung.template'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

bootJar {
    archiveFileName = 'app.jar'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:1.0.0"
    }
}

dependencies {
    // Spring Boot Starter
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Spring Devtools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // JWT (jsonwebtoken)
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // Spring Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Oracle
    runtimeOnly 'com.oracle.database.jdbc:ojdbc11'

    // MyBatis
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4'

    // S3
    implementation platform("software.amazon.awssdk:bom:2.25.28") // 버전은 최신으로 맞춰주세요
    implementation 'software.amazon.awssdk:s3'

    // Cloudfront
    implementation 'com.amazonaws:aws-java-sdk-cloudfront:1.12.679'

    // Kafka
    implementation 'org.springframework.kafka:spring-kafka'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    // JSON 직렬화
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

    // Elastic Search
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'

    // FCM
    implementation 'com.google.firebase:firebase-admin:9.2.0'

    // QR 코드 생성 (ZXing)
    implementation 'com.google.zxing:core:3.5.2'
    implementation 'com.google.zxing:javase:3.5.2'

    // mail
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // Spring AI (https://docs.spring.io/spring-ai/reference/api/chat/openai-chat.html?utm_source=chatgpt.com)
    implementation 'org.springframework.ai:spring-ai-openai'
    implementation 'org.springframework.ai:spring-ai-starter-model-openai' // auto configuration

    // Test
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // 분산락
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Websocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
}

tasks.named('test') {
    useJUnitPlatform()
}
